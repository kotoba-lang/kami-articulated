(ns articulated-transform-kotoba-golden-test
  (:require [clojure.java.shell :as shell]
            [clojure.string :as str]
            [clojure.test :refer [deftest is]]
            [kami_articulated :as articulated]
            [kotoba.compiler.core :as compiler]
            [kotoba.compiler.ir :as ir]))

(deftest articulated-transform-goldens-agree-across-targets
  (let [system (articulated/parse-urdf (slurp "test/fixtures/cartpole.urdf"))
        slider (articulated/joint-index system "slider_to_cart")
        revolute (articulated/joint-index system "cart_to_pole")
        slider-joint (nth (:joints system) slider)
        revolute-joint (nth (:joints system) revolute)
        angle 0.2 length 0.5 cart-coordinate 0.3
        [_ _ origin-z] (:xyz (:origin revolute-joint))
        expected [(max (:lower slider-joint) (min (:upper slider-joint) 3.0))
                  (+ cart-coordinate (* (Math/sin angle) length))
                  0.0
                  (+ origin-z (* (Math/cos angle) length))]
        source (slurp "src/articulated_transform_golden.kotoba")
        names ['bounded-slider-coordinate 'pole-tip-x 'pole-tip-y 'pole-tip-z]
        js-artifact (compiler/compile-source source :js-kotoba-v1)
        wasm-artifact (compiler/compile-source source :wasm32-browser-kotoba-v1)
        reference (mapv #(ir/execute (:kir js-artifact) % []) names)
        js64 (.encodeToString (java.util.Base64/getEncoder)
                              (.getBytes ^String (:source js-artifact) "UTF-8"))
        wasm64 (.encodeToString (java.util.Base64/getEncoder) (:bytes wasm-artifact))
        expected-js (str "[" (str/join "," (map #(Double/toString (double %)) expected)) "]")
        names-js (str "[" (str/join "," (map #(str "\"" % "\"") names)) "]")
        node-source
        (str "const expected=" expected-js ",names=" names-js ";"
             "const close=(a,b)=>Math.abs(a-b)<=1e-12;"
             "Promise.all([import('data:text/javascript;base64," js64 "'),"
             "WebAssembly.instantiate(Buffer.from('" wasm64 "','base64'),{})]).then(([j,w])=>{"
             "const a=j.instantiateKotoba({}),b=w.instance.exports;"
             "const js=names.map(n=>a[n]()),wa=names.map(n=>b[n]());"
             "if(!js.every((v,i)=>close(v,expected[i])&&Object.is(v,wa[i])))process.exit(2);"
             "}).catch(e=>{console.error(e);process.exit(99)})")
        node-result (shell/sh "node" "--input-type=module" "-e" node-source)]
    (is (= [1.0 0.0 0.0] (:axis slider-joint)))
    (is (= [0.0 1.0 0.0] (:axis revolute-joint)))
    (is (every? true? (map #(< (Math/abs (- %1 %2)) 1.0e-12) reference expected)))
    (is (zero? (:exit node-result)) (:err node-result))
    (is (= :kotoba.floating-point/ieee-754-f32-f64-v7
           (:floating-point-policy js-artifact)))
    (is (= #{} (set (:effects (:kir js-artifact)))))))
